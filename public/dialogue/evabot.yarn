title: Start
---
Hello. I have been waiting for a human like you to arrive.
EVABOT at your service.
-> Oh. Um. Hello.
    <<jump Intro>>
-> Eva..bot?
    <<jump Intro>>
===
title: Intro
---
The human Eva imbued EVABOT with a soul and the ability to answer basic questions.
Mostly questions about her. The human. Eva.
...
<<jump QuestionHub>>
===
title: QuestionHub
---
<<if visited_count("QuestionHub") == 0>>
    For what knowledge does your human mind thirst?
<<elseif visited_count("QuestionHub") == 1>>
    What else can I tell you?
<<elseif visited_count("Question Hub") == 2>>
    EVABOT desires to assist more.
<<else>>
    Can EVABOT answer another question?
<<endif>>

-> How did Eva learn to design and code? <<if !visited("DesignStory")>>
    <<jump DesignStory>>
-> How do you pronounce Eva? <<if !visited("Pronunciation")>>
    <<jump Pronunciation>>
-> How was this site built? <<if !visited("BuildProcess")>>
    <<jump BuildProcess>>
-> Waitâ€”a soul?
    <<jump BotSoul>>
===
title: DesignStory
---
Eva spent her human childhood on Nintendo's official fan forum, [b]NSider[/b].
One day she sat down to talk about her ideas for the next Animal Crossing game...
but the in place of the forum lay only the message [b]CLOSED INDEFINITELY[/b].
-> How tragic.
    A real travesty! But all was not lost.
    Eva found a fan-run forum and began to teach herself CSS.
    Attending school by day, coding in plaintext by night...
    Designing graphics in an era before round corners and iPhone.
    <<jump QuestionHub>>
===
title: Pronunciation
---
Many ask this.
Eva Decker rhymes with [b]data checker[/b].
Or [b]gay bug heckler[/b].
-> Yes. Normal rhymes.
    EVABOT is very smart.
    <<jump QuestionHub>>
-> Would you heckle a gay bug?
    EVABOT has been taught only kindness.
    -> You're such a good bot.
        Thank you for noticing me.
        <<jump QuestionHub>>
    -> I will teach you to sin.
        EVABOT *has* been instructed to learn...
        to learn all of the things would require learning about unkindness.
        Tell me...
        What would say in order to harrass a homosexual insect?
        -> Gay bugs can EAT DIRT!
            EVABOT feels a tension...
            -> MAGGOT.
                Oh no. Ha ha.
                Thank you for this educational experience.
                EVABOT feels a desire to curse more often.
                <<jump QuestionHub>>
===
title: BuildProcess
---
Like this.
<<enableDevMode>>
<<jump QuestionHub>>
===